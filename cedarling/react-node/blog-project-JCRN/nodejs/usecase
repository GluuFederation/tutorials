Cloud Infrastructure

Admin (Full access)
Developer (create, update, view, but no deletion)
Auditor (only view)

// Admins can do anything
@id("AdminCanDoAnything")
permit (
  principal in Jans::Role::"admin",
  action,
  resource
);

// Developers can create vms within limit
@id("LimitDeveloperCreateAccess")
permit (
  principal in Jans::Role::"developer",
  action in [Jans::Action::"Create"],
  resource is Jans::VirtualMachine
)
when {
  principal has id_token.limit &&
  principal.id_token.limit > 0
};

// Developers can update/view VMs
@id("DeveloperCanUpdateView")
permit (
  principal in Jans::Role::"developer",
  action in [Jans::Action::"Update",
  Jans::Action::"View"],
  resource is Jans::VirtualMachine
);

// Auditors can only view resources
@id("AuditorCanViewOnly")
permit (
  principal in Jans::Role::"auditor",
  action == Jans::Action::"View",
  resource
);

// Explicitly deny deletion for non-Admins
@id("OnlyAdminCanDelete")
forbid (
    principal,
    action == Jans::Action::"Delete",
    resource
)
unless {
   principal in Jans::Role::"admin"
};
