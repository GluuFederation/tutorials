Blog Platform

Roles: Admin, Editor, Author
Actions: Create, Edit, Delete, View, Conversation, GenerateImage, GenerateVideo
Resources: Article, AItools
Plan: basic, premium

// Admin can do anything
@id("AdminCanDoAnything")
permit (
  principal in Jans::Role::"Admin",
  action,
  resource
);

// Author can create, edit, delete, and view articles
@id("AuthorCanCreateEditDeleteView")
permit (
  principal in Jans::Role::"Author",
  action in [Jans::Action::"Create",
  Jans::Action::"Edit",
  Jans::Action::"Delete",
  Jans::Action::"View"],
  resource is Jans::Article
);

// Editor can edit and view articles but cannot create or delete them
@id("EditorCanEditViewArticleNotCreate")
permit (
  principal in Jans::Role::"Editor",
  action in [Jans::Action::"Edit",
  Jans::Action::"View"],
  resource is Jans::Article
);

// Basic plan users can access conversation tool
@id("BasicPlanAccessAIConversationTool")
permit (
  principal,
  action in [Jans::Action::"Conversation"],
  resource is Jans::AItools
)
when {
    principal has plan
};


// Premium plan users can access image and video generation tools
@id("PremiumPlanAccessAIGenerateImageVideoTool")
permit (
  principal,
  action in [Jans::Action::"GenerateImage",
  Jans::Action::"GenerateVideo"],
  resource is Jans::AItools
)
when {
    principal has plan && principal.plan == "premium"
};

